#
# qmake configuration for emscripten
#

MAKEFILE_GENERATOR      = UNIX
QMAKE_PLATFORM          = emscripten
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

QMAKE_INCDIR = $$(EMSCRIPTEN)/system/include

QT_QPA_DEFAULT_PLATFORM = eglfs

include(../../common/linux.conf)

# try 32 bit
QMAKE_CFLAGS            = -m32 -D__SSE__ -D__SSE2__
QMAKE_LFLAGS            = -m32

include(../../common/gcc-base-unix.conf)
include(../../common/clang.conf)

# emscripten

QMAKE_CC                = emcc
QMAKE_CXX               = em++
QMAKE_AR                = emar r

QMAKE_LINK_C            = $$QMAKE_CC
QMAKE_LINK_C_SHLIB      = $$QMAKE_CC

QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX

# Wrapper tools that understand .o/.a files with LLVM bytecode instead of machine code
QMAKE_AR_LTCG           = emar cqs
QMAKE_NM_LTCG           = llvm-nm -P

# ERROR_ON_UNDEFINED_SYMBOLS=1 so configure tests fail if symbols aren't found
QMAKE_LFLAGS += -Wl
QMAKE_LFLAGS += \
    -s RESERVED_FUNCTION_POINTERS=400\
    -s ALLOW_MEMORY_GROWTH=1 \
#    -s TOTAL_MEMORY=134217728\ #128MB
    -s FULL_ES2=1 \
#    -s INCLUDE_FULL_LIBRARY=1 \
    -s ERROR_ON_UNDEFINED_SYMBOLS=1 \


CONFIG *= emscripten
QMAKE_EXTENSION_EXE = js
# precompile headers needs testing
#CONFIG -= precompile_header

load(qt_config)
